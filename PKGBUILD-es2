# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.es2
pkgrel=1
pkgdesc="C++11 and OpenGL 2D/3D graphics engine (OpenGL ES 2.0 version)"
arch=('i686' 'x86_64')
url="https://github.com/mosra/magnum"
license=('MIT')
depends=('corrade' 'openal')
makedepends=('cmake')
options=(!strip)
provides=('magnum-git')

build() {
    mkdir -p "$startdir/build-es2"
    cd "$startdir/build-es2"

    if [ "$CXX" = clang++ ] ; then
        newcxxflags=$(echo $CXXFLAGS | sed s/--param=ssp-buffer-size=4//g)
        export CXXFLAGS="$newcxxflags"
    fi

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -DTARGET_GLES=ON \
        -DTARGET_GLES2=ON \
        -DWITH_MAGNUMINFO=OFF \
        -DWITH_XEGLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON
    make
}

check() {
    cd "$startdir/build-es2"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-es2"
    make DESTDIR="$pkgdir/" install
}
