# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.es3desktop
pkgrel=1
pkgdesc="C++11/C++14 graphics middleware for games and data visualization (desktop OpenGL ES 3.0 version)"
arch=('i686' 'x86_64')
url="https://magnum.graphics"
license=('MIT')
depends=('corrade' 'openal' 'sdl2' 'glfw')
makedepends=('cmake' 'ninja')
options=('!strip')
provides=('magnum-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-es3desktop"
    cd "$_rootdir/build-es3desktop"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTARGET_GLES=ON \
        -DTARGET_GLES2=OFF \
        -DTARGET_DESKTOP_GLES=ON \
        -DWITH_AUDIO=ON \
        -DWITH_GLFWAPPLICATION=ON \
        -DWITH_GLXAPPLICATION=ON \
        -DWITH_SDL2APPLICATION=ON \
        -DWITH_XEGLAPPLICATION=ON \
        -DWITH_WINDOWLESSGLXAPPLICATION=ON \
        -DWITH_EGLCONTEXT=ON \
        -DWITH_GLXCONTEXT=ON \
        -DWITH_OPENGLTESTER=ON \
        -DWITH_ANYAUDIOIMPORTER=ON \
        -DWITH_ANYIMAGECONVERTER=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENECONVERTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_ANYSHADERCONVERTER=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DWITH_IMAGECONVERTER=ON \
        -DWITH_SCENECONVERTER=ON \
        -DWITH_SHADERCONVERTER=ON \
        -DWITH_GL_INFO=ON \
        -DWITH_AL_INFO=ON \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -G Ninja
    ninja
}

check() {
    cd "$_rootdir/build-es3desktop"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5
    MAGNUM_DISABLE_EXTENSIONS="GL_NV_framebuffer_multisample GL_NV_framebuffer_blit GL_EXT_robustness" CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5 -R GLTest
    MAGNUM_DISABLE_EXTENSIONS="GL_KHR_debug" CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5 -R GLTest
}

package() {
    cd "$_rootdir/build-es3desktop"
    DESTDIR="$pkgdir/" ninja install
}
