# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum
pkgver=dev.webgl2
pkgrel=1
pkgdesc="C++11/C++14 and OpenGL 2D/3D graphics engine (Emscripten WebGL 2.0)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('emscripten-corrade')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-webgl2" ] ; then
        mkdir "$_rootdir/build-emscripten-webgl2"
        cd "$_rootdir/build-emscripten-webgl2"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -DCMAKE_PREFIX_PATH=/usr/lib/emscripten/system \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-webgl2"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DMAGNUM_DEPLOY_PREFIX=/srv/http/emscripten-webgl2 \
        -DTARGET_GLES2=OFF \
        -DWITH_AUDIO=ON \
        -DWITH_SDL2APPLICATION=ON \
        -WDITH_WINDOWLESSEGLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DWITH_MAGNUMINFO=ON \
        -DWITH_AL_INFO=ON \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-webgl2"
    CORRADE_TEST_COLOR=ON ctest -j5 --output-on-failure -E ALTest
}

package() {
    cd "$_rootdir/build-emscripten-webgl2"
    DESTDIR="$pkgdir/" ninja install
}
