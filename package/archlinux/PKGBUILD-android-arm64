# Author: mosra <mosra@centrum.cz>
pkgname=android-arm64-magnum
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 graphics middleware for games and data visualization (Android ARM64)"
arch=('any')
url="https://magnum.graphics"
license=('MIT')
depends=('android-arm64-corrade')
makedepends=('cmake>=3.20' 'ninja' 'android-ndk' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-arm64" ] ; then
        mkdir "$_rootdir/build-android-arm64"
        cd "$_rootdir/build-android-arm64"

        cmake .. \
            -DCMAKE_SYSTEM_NAME=Android \
            -DCMAKE_SYSTEM_VERSION=24 \
            -DCMAKE_ANDROID_ARCH_ABI=arm64-v8a \
            -DCMAKE_ANDROID_STL_TYPE=c++_static \
            -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/toolchains/llvm/prebuilt/linux-x86_64/sysroot/usr \
            -G Ninja
    fi

    cd "$_rootdir/build-android-arm64"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DWITH_VK=ON \
        -DWITH_ANYAUDIOIMPORTER=OFF \
        -DWITH_ANYIMAGECONVERTER=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENECONVERTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_ANYSHADERCONVERTER=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_ANDROIDAPPLICATION=ON \
        -DWITH_EGLCONTEXT=ON \
        -DWITH_WINDOWLESSEGLAPPLICATION=ON \
        -DWITH_GL_INFO=ON \
        -DWITH_VK_INFO=ON \
        -DTARGET_GLES2=OFF \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -DBUILD_VK_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-arm64"
    DESTDIR="$pkgdir/" ninja install/strip
}

# kate: hl bash
