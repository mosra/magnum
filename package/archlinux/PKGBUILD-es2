# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.es2
pkgrel=1
pkgdesc="C++11/C++14 and OpenGL 2D/3D graphics engine (OpenGL ES 2.0 version)"
arch=('i686' 'x86_64')
url="http://magnum.graphics"
license=('MIT')
depends=('corrade' 'openal')
makedepends=('cmake' 'ninja')
options=('!strip')
provides=('magnum-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-es2"
    cd "$_rootdir/build-es2"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTARGET_GLES=ON \
        -DTARGET_GLES2=ON \
        -DWITH_AUDIO=ON \
        -DWITH_XEGLAPPLICATION=ON \
        -DWITH_WINDOWLESSEGLAPPLICATION=ON \
        -DWITH_EGLCONTEXT=ON \
        -DWITH_OPENGLTESTER=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DWITH_IMAGECONVERTER=ON \
        -DWITH_MAGNUMINFO=ON \
        -DWITH_AL_INFO=ON \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -G Ninja
    ninja
}

check() {
    cd "$_rootdir/build-es2"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5
    MAGNUM_DISABLE_EXTENSIONS="GL_NV_framebuffer_multisample GL_NV_framebuffer_blit GL_EXT_robustness" CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5 -R GLTest
    MAGNUM_DISABLE_EXTENSIONS="GL_KHR_debug" CORRADE_TEST_COLOR=ON ctest --output-on-failure -j5 -R GLTest
}

package() {
    cd "$_rootdir/build-es2"
    DESTDIR="$pkgdir/" ninja install
}
