# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.gcc47
pkgrel=1
pkgdesc="C++11 and OpenGL 2D/3D graphics engine (built with GCC 4.7)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('corrade' 'openal' 'freeglut' 'sdl2')
makedepends=('cmake' 'ninja' 'gcc47')
options=('!strip' 'staticlibs')
provides=('magnum-git')

build() {
    if [ ! -d "$startdir/build-gcc47" ] ; then
        mkdir "$startdir/build-gcc47"
        cd "$startdir/build-gcc47"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.7 \
            -G Ninja
    fi

    cd "$startdir/build-gcc47"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_AUDIO=ON \
        -DWITH_GLUTAPPLICATION=ON \
        -DWITH_GLXAPPLICATION=ON \
        -DWITH_SDL2APPLICATION=ON \
        -DWITH_WINDOWLESSGLXAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DWITH_DISTANCEFIELDCONVERTER=ON \
        -DWITH_FONTCONVERTER=ON \
        -DWITH_MAGNUMINFO=ON \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON
    ninja
}

check() {
    cd "$startdir/build-gcc47"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-gcc47"
    DESTDIR="$pkgdir/" ninja install
}
