# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.clang.libc++
pkgrel=1
pkgdesc="C++11 and OpenGL 2D/3D graphics engine (built with clang and libc++)"
arch=('i686' 'x86_64')
url="https://github.com/mosra/magnum"
license=('MIT')
depends=('corrade' 'openal' 'freeglut' 'sdl2' 'libc++')
makedepends=('cmake' 'clang' 'ninja')
options=(!strip)
provides=('magnum-git')

build() {
    if [ ! -d "$startdir/build-clang-libc++" ] ; then
        mkdir "$startdir/build-clang-libc++"
        cd "$startdir/build-clang-libc++"

        cmake .. \
            -DCMAKE_CXX_COMPILER=clang++ \
            -DCMAKE_CXX_FLAGS="-std=c++11 -stdlib=libc++" \
            -DCMAKE_EXE_LINKER_FLAGS=-lc++abi \
            -G Ninja
    fi

    cd "$startdir/build-clang-libc++"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_AUDIO=ON \
        -DWITH_GLUTAPPLICATION=ON \
        -DWITH_GLXAPPLICATION=ON \
        -DWITH_SDL2APPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DWITH_DISTANCEFIELDCONVERTER=ON \
        -DWITH_FONTCONVERTER=ON \
        -DWITH_MAGNUMINFO=ON \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON
    ninja
}

check() {
    cd "$startdir/build-clang-libc++"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-clang-libc++"
    DESTDIR="$pkgdir/" ninja install
}
