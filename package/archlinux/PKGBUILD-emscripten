# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 and OpenGL 2D/3D graphics engine (Emscripten)"
arch=('any')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('emscripten-corrade')
makedepends=('cmake' 'emscripten' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten" ] ; then
        mkdir "$_rootdir/build-emscripten"
        cd "$_rootdir/build-emscripten"

        cmake .. \
            -DCMAKE_MODULE_PATH="$_rootdir/toolchains/modules" \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -DCMAKE_PREFIX_PATH=/usr/lib/emscripten/system
    fi

    cd "$_rootdir/build-emscripten"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DWITH_SDL2APPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON
    make
}

package() {
    cd "$_rootdir/build-emscripten"
    DESTDIR="$pkgdir/" make install
}
