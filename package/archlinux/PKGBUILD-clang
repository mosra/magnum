# Author: mosra <mosra@centrum.cz>
pkgname=magnum
pkgver=dev.clang
pkgrel=1
pkgdesc="C++11 and OpenGL 2D/3D graphics engine (built with clang)"
arch=('i686' 'x86_64')
url="https://github.com/mosra/magnum"
license=('MIT')
depends=('corrade' 'openal' 'freeglut' 'sdl2')
makedepends=('cmake' 'clang' 'ninja')
options=(!strip)
provides=('magnum-git')

build() {
    if [ ! -d "$startdir/build-clang" ] ; then
        mkdir "$startdir/build-clang"
        cd "$startdir/build-clang"

        cmake .. \
            -DCMAKE_CXX_COMPILER=clang++ \
            -G Ninja
    fi

    cd "$startdir/build-clang"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_GLUTAPPLICATION=ON \
        -DWITH_GLXAPPLICATION=ON \
        -DWITH_SDL2APPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DBUILD_TESTS=TRUE
    ninja
}

check() {
    cd "$startdir/build-clang"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-clang"
    DESTDIR="$pkgdir/" ninja install
}
