# Author: williamjcm <w.jcm59@gmail.com>
# Contributor: mosra <mosra@centrum.cz>
# Based on the packages/archlinux/PKGBUILD file by mosra <mosra@centrum.cz> and the MSYS2/MinGW PKGBUILD templates

_realname=magnum
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 graphics middleware for games and data visualization"
arch=('any')
url="https://magnum.graphics/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-corrade"
         "${MINGW_PACKAGE_PREFIX}-openal"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-glfw"
         "${MINGW_PACKAGE_PREFIX}-vulkan-loader")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-ninja")
options=('!strip')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-${CARCH}"
    cd "$_rootdir/build-${CARCH}"

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        ${MINGW_PREFIX}/bin/cmake .. \
            -G'Ninja' \
            -DCMAKE_BUILD_TYPE=Debug \
            -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
            -DWITH_AUDIO=ON \
            -DWITH_VK=ON \
            -DWITH_GLFWAPPLICATION=ON \
            -DWITH_SDL2APPLICATION=ON \
            -DWITH_WINDOWLESSWGLAPPLICATION=ON \
            -DWITH_WGLCONTEXT=ON \
            -DWITH_OPENGLTESTER=ON \
            -DWITH_ANYAUDIOIMPORTER=ON \
            -DWITH_ANYIMAGECONVERTER=ON \
            -DWITH_ANYIMAGEIMPORTER=ON \
            -DWITH_ANYSCENECONVERTER=ON \
            -DWITH_ANYSCENEIMPORTER=ON \
            -DWITH_MAGNUMFONT=ON \
            -DWITH_MAGNUMFONTCONVERTER=ON \
            -DWITH_OBJIMPORTER=ON \
            -DWITH_TGAIMAGECONVERTER=ON \
            -DWITH_TGAIMPORTER=ON \
            -DWITH_WAVAUDIOIMPORTER=ON \
            -DWITH_DISTANCEFIELDCONVERTER=ON \
            -DWITH_FONTCONVERTER=ON \
            -DWITH_IMAGECONVERTER=ON \
            -DWITH_SCENECONVERTER=ON \
            -DWITH_GL_INFO=ON \
            -DWITH_AL_INFO=ON
    ninja
}

package() {
    cd "$_rootdir/build-${CARCH}"
    DESTDIR="${pkgdir}" ninja install
}
